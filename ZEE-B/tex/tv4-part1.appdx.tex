\subsection{Ringradien gegen Ringenindizes}
    \label{appdx:tv4-part1}
    {  
        % % Surpress "errors" in minted
        % https://tex.stackexchange.com/a/289068
        \renewcommand{\fcolorbox}[4][]{#4}
        \begin{minted}[linenos,breaklines,autogobble,frame=leftline,framesep=10pt]{gnuplot}
#!/usr/bin/env gnuplot
# Version >= 5.2

lambdaminus = 1 # bzw. 0

if (lambdaminus) {
    set term epslatex color size 7in, 4.5in
    set output "tv4-l-minus.tex"

    set title "Verlauf der Radien mit Ringenindizes ($\\lambda_-$)"
} else {
    set term epslatex color size 7in, 4.5in
    set output "tv4-l-plus.tex"

    set title "Verlauf der Radien mit Ringenindizes ($\\lambda_+$)"
}

set decimalsign ","

set ylabel "Radien$^2$ $r_m^2/\\si{\\micro\\meter\\squared}$"
set xlabel "Ringindex $p$ (Einheitlos)"

set mxtics
set mytics
set samples 10000

f(x) = m*x + c # Linear fit

array A_m[5]
array A_m_err[5]
array A_c[5]
array A_c_err[5]
array chisq[5]
array titel[5]
array input_mp[5]
array titel_mp[5]
array A_p0[5]
array A_p0_err[5]

# http://gnuplot.info/demo_5.4/array.1.gnu
array strom[5]     = ["2.495", "4.190", "5.662", "7.01", "8.78"]
array strom_err[5] = ["5", "10", "10", "1", "1"]

# https://stackoverflow.com/a/17884635
do for [t=1:5] {
    inp = "tv4-".t.".dat"
    input_mp[t] = inp
    titel_mp[t] = "$I = \\SI{".strom[t]."(".strom_err[t].")}{\\ampere}$"

    m = 1; c = 1;
    if (lambdaminus) {
        fit f(x) inp u 1:($2*$2):(2*$2*$3) yerrors via m,c
    } else {
        fit f(x) inp u 1:($4*$4):(2*$4*$5) yerrors via m,c
    }
    
    A_m[t] = m
    A_m_err[t] = m_err
    A_c[t] = c
    A_c_err[t] = c_err
    chisq[t] = FIT_STDFIT**2
    titel[t] = "$".gprintf("%.5f", m)."p + (".gprintf("%.5f", c).")$"

    A_p0[t] = -c/m
    A_p0_err[t] = abs(A_p0[t]) * sqrt((c_err/c)**2 + (m_err/m)**2)
}

set key bottom right vertical maxrows 5 width -7


if (lambdaminus) {
    plot for [i=1:5] input_mp[i] u 1:($2*$2):(2*$2*$3) with yerrorbars title titel_mp[i] pointtype 77 lc i, for [i=1:5] A_m[i]*x+A_c[i] title titel[i] lc i
} else {
    plot for [i=1:5] input_mp[i] u 1:($4*$4):(2*$4*$5) with yerrorbars title titel_mp[i] pointtype 77 lc i, for [i=1:5] A_m[i]*x+A_c[i] title titel[i] lc i
}

print ""
if (lambdaminus) { print "lambda-" } else { print "lambda+" }

# Raw data output
print A_m
print A_m_err

# LaTeX table output
print "\\toprule"
print "Strom $I/\\si{\\ampere}$ & $m/\\si{\\micro\\meter\\squared}$ & $c/\\si{\\micro\\meter\\squared}$ & $p_0$ & $\\chi^2_\\text{red}$ \\\\"
print "\\midrule"
do for [t=1:5] {
    print "\t\\num{".strom[t]."(".strom_err[t].")} & \\num{".gprintf("%.5f", A_m[t])."(".gprintf("%.0f", A_m_err[t]*10**5).")} & \\num{".gprintf("%.5f", A_c[t])."(".gprintf("%.0f", A_c_err[t]*10**5).")}"." & \\num{".gprintf("%.5f", A_p0[t])."(".gprintf("%.0f", A_p0_err[t]*10**5).")} & \\num{".gprintf("%.5f", chisq[t])."} \\\\"
}
print "\\bottomrule"
print ""

# Raw data output in table form
print "# Nr\tm/um^2 \tm_err/um^2\tc/um^2 \t c_err/um^2 \t p \t p_err"
do for [t=1:5] {
    print "".t."\t".sprintf("%.10f", A_m[t])."\t".sprintf("%.10f", A_m_err[t])."\t".sprintf("%.10f", A_c[t])."\t".sprintf("%.10f", A_c_err[t])."\t".sprintf("%.10f", A_p0[t])."\t".sprintf("%.10f", A_p0_err[t])
}
        \end{minted}
    }
    mit
    \begin{multicols}{2}
        \texttt{tv4-1.dat}:
        \begin{minted}[linenos,breaklines,autogobble,frame=leftline,framesep=10pt]{text}
#   lambda -    lambda +
# p r/um    dr  r/um    dr
1   48,30   3   58,70   3
2   101,59  2,5 107,02  2,5
3   135,36  2   139,63  2
4   161,72  1,5 164,65  1,5
5   183,72  1,5 185,69  1,5
        \end{minted}
        \texttt{tv4-2.dat}:
        \begin{minted}[linenos,breaklines,autogobble,frame=leftline,framesep=10pt]{text}
#   lambda -    lambda +
# p r/um    dr  r/um    dr
1   43,85   4   62,07   4
2   99,09   3   108,51  3
3   134,89  3   140,23  2
4   160,43  1,5 165,42  1,5
5   182,71  1,5 187,02  1,5
        \end{minted}
        \texttt{tv4-3.dat}:
        \begin{minted}[linenos,breaklines,autogobble,frame=leftline,framesep=10pt]{text}
#   lambda -    lambda +
# p r/um    dr  r/um    dr
1   39,26   5   64,70   4
2   97,88   3   110,70  3
3   132,59  3   141,91  2
4   159,59  2   166,79  2
5   181,31  1,5 188,77  1,5
        \end{minted}
        \texttt{tv4-4.dat}:
        \begin{minted}[linenos,breaklines,autogobble,frame=leftline,framesep=10pt]{text}
#   lambda -    lambda +
# p r/um    dr  r/um    dr
1   35,45   6   66,38   4
2   96,26   3   111,61  3
3   130,98  2   143,36  2
4   158,46  2   168,27  1,5
5   180,88  1,5 189,16  1,5
        \end{minted}
    \end{multicols}
    \texttt{tv4-5.dat}:
        \begin{minted}[linenos,breaklines,autogobble,frame=leftline,framesep=10pt]{text}
#   lambda -    lambda +
# p r/um    dr  r/um    dr
1   30,79   7   69,88   5
2   94,15   3   113,33  3
3   129,67  2   144,56  2
4   157,19  2   168,98  1,5
5   179,64  1,5 190,10  2
        \end{minted}
    % \vspace{-\baselineskip}
    Rohausgabe:
    $\lambda_-$:
    \inputminted[linenos,breaklines,autogobble,frame=leftline,framesep=10pt]{text}{./plots/lambda_minus.output}
    $\lambda_+$:
    \inputminted[linenos,breaklines,autogobble,frame=leftline,framesep=10pt]{text}{./plots/lambda_plus.output}